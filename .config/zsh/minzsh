# simple zsh plugin manager
# rm -rf doesn't give an error if directory doesn't exist

[ -z $MINZSH_DIR ] && export MINZSH_DIR="${XDG_DATA_HOME:-$HOME/.local/share}/zsh/plugins"

mzls() {
  \ls -l $MINZSH_DIR | awk '{print $9}' 2> /dev/null || echo "No zsh plugin ever downloaded"
}

mzrm() {
  [ ! -z $1 ] && \rm -rf "$MINZSH_DIR/$1" || echo "Please specify the plugin you want to remove"
}

mzadd() {
  # reference:
  # https://unix.stackexchange.com/a/325492/479142
  # https://stackoverflow.com/a/66462684
  local plugin_name="${1##*/}"

  if [ -d "$MINZSH_DIR/$plugin_name" ]; then
    # source plugins
    {
      [ -f "$MINZSH_DIR/$plugin_name/$plugin_name.plugin.zsh" ] && source "$MINZSH_DIR/$plugin_name/$plugin_name.plugin.zsh"
    } || {
      [ -f "$MINZSH_DIR/$plugin_name/$plugin_name.zsh" ] && source "$MINZSH_DIR/$plugin_name/$plugin_name.zsh"
    } || {
      [ -f "$MINZSH_DIR/$plugin_name/$plugin_name.zsh-theme" ] && source "$MINZSH_DIR/$plugin_name/$plugin_name.zsh-theme"
    }

  # git clone plugin from github
  else
    git clone --depth 1 --single-branch --no-tags "https://github.com/$1.git" "$MINZSH_DIR/$plugin_name" && mzadd "$1" || echo "Failed to git clone https://github.com/$1.git"

  fi
}

mzupdate() {
  for d in $(\ls -d $MINZSH_DIR/*); do
    paste <(echo "${d##*/} ") <(git -C "$d" pull --no-stat)
  done
}

# vim:ft=sh
