# ref:
# https://salferrarello.com/zsh-git-status-prompt/
# https://github.com/zap-zsh/singularisart-prompt/blob/master/singularisart-prompt.zsh-theme
# https://github.com/zap-zsh/zap-prompt/blob/master/zap-prompt.zsh-theme

# autoload zsh add-zsh-hook and vcs_info functions (-U autoload w/o substition, -z use zsh style)
autoload -Uz add-zsh-hook vcs_info

# enable substitution in the prompt.
setopt prompt_subst

# run vcs_info just before a prompt is displayed (precmd)
add-zsh-hook precmd vcs_info

# ref: https://zsh.sourceforge.io/Doc/Release/User-Contributions.html#vcs_005finfo-Configuration
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' use-simple true
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' unstagedstr '%F{red}!%f'
zstyle ':vcs_info:*' stagedstr '%F{green}+%f'
zstyle ':vcs_info:git:*' formats ' %F{magenta}%b%f %u%c'
zstyle ':vcs_info:git:*' actionformats ' %F{yellow}(%a)%f %F{magenta}%10<..<%b%<<%f %u%c'

+vi-git-untracked() {
    if [[ $(git rev-parse --is-inside-work-tree 2>/dev/null) == 'true' ]] && \
      git status --porcelain 2>/dev/null | grep -q '^??'; then
        hook_com[unstaged]+='%F{yellow}?%f'
    fi
}

# ref: https://stackoverflow.com/a/53823705
+vi-git-stash() {
    if [[ $(git rev-parse --is-inside-work-tree 2>/dev/null) == 'true' ]] && \
      git rev-list --quiet refs/stash 2>/dev/null; then
        hook_com[branch]+=' %F{white}$%f'
    fi
}

zstyle ':vcs_info:git*+set-message:*' hooks git-stash git-untracked

# variables common to all prompt styles
prompt_newline=$'\n%{\r%}'

# add ${vcs_info_msg_0} to the prompt
# e.g. here we add the Git information in red
# shorten path ref: https://unix.stackexchange.com/a/273567
PROMPT='${prompt_newline}'
PROMPT+='%B%F{cyan}%(4~|%-1~/../%2~|%~)%f%b'
PROMPT+='%B${vcs_info_msg_0_}%b'
PROMPT+='${prompt_newline}%(?:%B%F{green}>%f%b :%B%F{red}x%f%b )'

# vim:ft=zsh
